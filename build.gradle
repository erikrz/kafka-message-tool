buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'checkstyle'
    id 'groovy'
    id 'idea'
    id "jacoco"
    id 'java'
    id 'java-library-distribution'
    id 'maven-publish'
    id 'publishing'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.github.spotbugs' version "5.0.7"
    id "com.jfrog.artifactory" version "4.28.3"
    id "io.freefair.lombok" version "6.4.3.1"
    id 'org.ajoberstar.grgit' version '5.0.0'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "org.sonarqube" version "3.4.0.2513"
}

repositories {
    mavenCentral()
}

import java.time.Instant

// <app>-<major-patch>-<minor-patch>-<fix>
//version = "1.1.x.x-dev-build.02"
version = "1.0.4.0"

println "Gradle project dir: ${projectDir}"

sourceCompatibility = 11
targetCompatibility = 11
ext.timestamp = Instant.now().toString()

distTar.enabled = false
distZip.enabled = false


if (version == "unspecified") {
    version = "dev"
}

jar.archiveFileName = "${rootProject.name}-${version}.jar"
jar.manifest.attributes 'Main-Class': 'application.Launcher'

dependencies {
    testImplementation(
            'com.github.grzesiek-galezowski:tdd-toolkit-java:1.2.5',
            'org.assertj:assertj-core:3.23.1',
            'org.hamcrest:hamcrest-all:1.3',
            'org.mockito:mockito-core:4.6.1',
            'org.testng:testng:7.6.0',
            'org.xmlunit:xmlunit-matchers:2.9.0'
    )
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.sun.xml.bind:jaxb-impl:4.0.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.kafka:kafka-clients:3.2.0'
    implementation 'org.apache.kafka:kafka_2.12:3.2.0'
    implementation 'org.codehaus.groovy:groovy-all:3.0.11'
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation 'org.fxmisc.richtext:richtextfx:0.10.9'
    implementation 'org.projectlombok:lombok:1.18.24'
}

checkstyle {
    toolVersion = "8.1"
}

task createProperties() {
    doLast {
        String fileLocation = "$projectDir/src/main/resources/version.properties"
        // recreate file on build, delete it first
        new File(fileLocation).delete()
        new File(fileLocation).withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

processResources.dependsOn createProperties

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing' ]
}

spotbugs {
    toolVersion = "4.7.0"
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    omitVisitors = ["FindNonShortCircuit"]
    excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
}

task checkAndTest {
    group = rootProject.name
    dependsOn tasks.check, tasks.test
}

task buildJar {
    group = rootProject.name
    dependsOn tasks.assemble, tasks.checkAndTest, tasks.shadowJar
}

shadowJar {
    archiveBaseName.set(project.name)
    version = project.version
    archiveAppendix.set("")
    archiveClassifier.set("")
    from("src/main/java") {
        include "/**/*.fxml"
    }
}

jar {
    archivesBaseName = project.name
    from("src/main/java") {
        include '/**/*.fxml'
    }
}

build.doFirst {
    println "Build Version [${version}] ISO Time [${timestamp}]"
}

test {
    useTestNG() {
        useDefaultListeners = true
    }
}

task testWithJacocoReport {
    group = rootProject.name
    dependsOn tasks.checkAndTest, jacocoTestReport
    jacocoTestReport.mustRunAfter checkAndTest
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

build.dependsOn shadowJar
